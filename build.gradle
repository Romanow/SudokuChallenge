plugins {
    id "java"
    id "idea"
    id "org.springframework.boot" version "2.4.5"
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
}

ext["junit-jupiter.version"] = "5.6.0"
ext["jetbrainsAnnotationVersion"] = "22.0.0"
ext["mapstructVersion"] = "1.5.5.Final"

sourceCompatibility = "11"
targetCompatibility = "11"

repositories {
    mavenCentral()
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

idea {
    module {
        testSourceDirs += file("src/integrationTest/java")
    }
}

tasks.register("integrationTest", Test) {
    description = "Runs integration tests."
    group = "verification"

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

check.dependsOn integrationTest

dependencies {
    // We will use GSON instead of Jackson
    implementation "com.google.code.gson:gson"

    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-logging"
    implementation "org.jetbrains:annotations:$jetbrainsAnnotationVersion"

    runtimeOnly "com.h2database:h2"

    compileOnly "org.projectlombok:lombok"
    annotationProcessor "org.projectlombok:lombok"
    implementation "org.mapstruct:mapstruct:$mapstructVersion"
    annotationProcessor "org.mapstruct:mapstruct-processor:$mapstructVersion"

    testImplementation "org.junit.jupiter:junit-jupiter-api"
    testImplementation "org.junit.jupiter:junit-jupiter-engine"
    testImplementation "org.junit.jupiter:junit-jupiter-params"
    testImplementation "org.mockito:mockito-core"
    testImplementation "org.assertj:assertj-core"

    integrationTestImplementation "org.springframework.boot:spring-boot-starter-test"
    integrationTestRuntimeOnly "com.h2database:h2"
}

test {
    useJUnitPlatform()
}

integrationTest {
    useJUnitPlatform()
}